import funkin.graphics.FunkinCamera;
import funkin.graphics.shaders.GaussianBlurShader;
import funkin.input.Cursor;
import funkin.modding.base.ScriptedMusicBeatSubState;
import funkin.save.Save;
import funkin.util.FileUtilSandboxed;
import funkin.util.ReflectUtil;

import flixel.FlxG;

import openfl.filters.ShaderFilter;

import json.util.TypeUtil;

import haxe.ds.StringMap;
import haxe.ui.RuntimeComponentBuilder;
import haxe.ui.components.Button;
import haxe.ui.components.Label;
import haxe.ui.components.NumberStepper;
import haxe.ui.components.Switch;
import haxe.ui.containers.HBox;
import haxe.ui.containers.ScrollView;
import haxe.ui.events.MouseEvent;
import haxe.ui.events.UIEvent;
import haxe.ui.util.MathUtil;

class ModConfigSubState extends ScriptedMusicBeatSubState {
    var mapsTools:MapsTools = new MapsTools();
    var jsonCfg:JsonConfig;
    var funkConfig:Dynamic;

    var configValues:StringMap<String>;
    var modid:String;
    var configCam:FunkinCamera = new FunkinCamera("configCam");

    var configTitle:Label = new Label();
    var configView:ScrollView = new ScrollView();

    var backButton:Button = new Button();

    var prevPersUpd:Bool;

    function new(config:Configuration) {
        super();
        configValues = mapsTools.stringToStringMap(config.configName);
        modid = config.modid;
    }

    override function create() {
        super.create();

        prevPersUpd = FlxG.state.persistentUpdate;
        FlxG.state.persistentUpdate = false;

        if (!configValues.keys().hasNext()) trace("Mod config is empty, please add one !");

        for (cfg in configValues.keyValueIterator()) {
            switch (cfg.key) {
                case "json": handleJsonConfig(cfg.value);
                case "funkin": handleFunkConfig(cfg.value);
                default: close();
            }
        }

        setupCamera();
        setupUI();

        Cursor.show();
    }

    override function update(elapsed:Float) {
        super.update(elapsed);

        if (controls.BACK) switchSubState(ScriptedMusicBeatSubState.init("ModMenuSubState"));
    }

    // Setup methods
    function setupCamera() {
        FlxG.state.camera.filters = [new ShaderFilter(new GaussianBlurShader(1))];
        configCam.bgColor = 0x5F000000;
        FlxG.cameras.add(configCam, false);
    }

    function setupUI() {
        var configMenuUI = RuntimeComponentBuilder.build(Paths.ui("mod-menu/config-ui"));
        if (configMenuUI == null) return;
        configMenuUI.camera = configCam;

        configTitle.camera = configCam;
        configTitle.addClass("configTitle");
        configTitle.text = modid + " Config";
        configTitle.top = 25;
        add(configTitle);

        configView.id = "configView";
        configView.camera = configCam;
        configView.percentWidth = 100;
        configView.top = 70;
        configView.percentHeight = 80;
        add(configView);

        backButton.id = "configButton";
        backButton.camera = configCam;
        backButton.text = "Exit Config";
        backButton.top = configView.top + 600;
        backButton.left = (FlxG.width - 257) * 0.5;
        backButton.onClick = (event:MouseEvent) -> switchSubState(ScriptedMusicBeatSubState.init("ModMenuSubState"));
        add(backButton);
    }

    // Handler methods
    function handleJsonConfig(path:String) {
        jsonCfg = new JsonConfig();
        jsonCfg.setFilePath(path);

        exploreConfigStructure("json");
    }

    function handleFunkConfig(value:String) {
        trace("FUNKIN CONFIG DETECTED!");

        funkConfig = Save.instance.modOptions.get(value);
        if (funkConfig == null) {
            trace("Funkin config not found for: " + value);
            return;
        }

        exploreConfigStructure("funkin");
    }

    // Helper methods
    function exploreConfigStructure(configType:String, ?path:Array<String>) {
        if (path == null) path = [];
        var currentValue = getCurrentValue(configType, path);
        if (currentValue == null) return;

        for (field in ReflectUtil.getFieldsOf(currentValue)) {
            var newPath = path.copy();
            newPath.push(field);
            var value = getCurrentValue(configType, newPath);

            var pathString = newPath.join(" → ");
            //trace("Path: " + pathString);

            if (TypeUtil.isInt(value)) {
                createIntegerControl(newPath, value, configType);
                //trace("  Type: INT, Value: " + value);
            } else if (TypeUtil.isBool(value)) {
                createBoolControl(newPath, value, configType);
                //trace("  Type: BOOL, Value: " + value);
            } else if (TypeUtil.isFloat(value)) {
                createFloatControl(newPath, value, configType);
                //trace("  Type: FLOAT, Value: " + value);
            } else if (TypeUtil.isString(value)) {
                //trace("  Type: STRING, Value: " + value);
            } else if (TypeUtil.isArray(value)) {
                //trace("  Type: ARRAY, Length: " + value.length);
            } else if (TypeUtil.isNull(value)) {
                //trace("  Type: NULL");
            } else if (ReflectUtil.isObject(value)) {
                //trace("  Type: OBJECT");
                // Recursively explore this object
                exploreConfigStructure(configType, newPath);
            }
        }
    }

    function createIntegerControl(path, value, configType:String) {
        var persPath = path;
        var persConfigType = configType;
        var container = createControlContainer(path, "intContainer");
        var input:NumberStepper = new NumberStepper();
        input.addClass("intStepper");
        input.value = value;
        input.step = 1;
        input.max = 2147483647;
        input.min = -2147483648;
        input.onChange = (event:UIEvent) -> updateConfigValue(persPath, Std.int(input.value), persConfigType);

        container.addComponent(input);
        configView.addComponent(container);
    }

    function createBoolControl(path, value, configType:String) {
        var persPath = path;
        var persConfigType = configType;
        var container = createControlContainer(path, "boolContainer");
        var input:Switch = new Switch();
        input.addClass("boolSwitch");
        input.addClass("pill-switch");
        input.value = value;
        input.onChange = (event:UIEvent) -> updateConfigValue(persPath, input.value, persConfigType);

        container.addComponent(input);
        configView.addComponent(container);
    }

    function createFloatControl(path, value, configType:String) {
        var persPath = path;
        var persConfigType = configType;
        var container = createControlContainer(path, "intContainer");
        var input:NumberStepper = new NumberStepper();
        input.addClass("floatStepper");
        input.value = value;
        var precision = MathUtil.precision(value);
        input.step = Math.pow(10, -precision);
        input.precision = precision;
        input.onChange = (event:UIEvent) -> updateConfigValue(persPath, input.value, persConfigType);

        container.addComponent(input);
        configView.addComponent(container);
    }

    function createControlContainer(path:Array<String>, clsName:String):HBox {
        var container:HBox = new HBox();
        container.addClass(clsName);

        // Add field label
        var fieldLabel:Label = new Label();
        fieldLabel.addClass("configLabel");
        fieldLabel.text = path[path.length - 1] + ":";
        container.addComponent(fieldLabel);

        return container;
    }

    function updateConfigValue(path:Array<String>, newValue:Dynamic, configType:String) {
        switch (configType) {
            case "json": jsonCfg.setDeepConfigValue(path, newValue);
            case "funkin": setDeepFunkValue(path, newValue);
        }

        trace("Updated " + path.join(" → ") + " to: " + newValue);
    }

    // Setter/Getter
    function getCurrentValue(configType:String, path:Array<String>):Dynamic {
        return switch (configType) {
            case "json": (path.length == 0) ? jsonCfg.getConfig() : jsonCfg.getDeepConfigValue(path);
            case "funkin": (path.length == 0) ? funkConfig : getDeepFunkValue(path);
            default: null;
        }
    }

    function getDeepFunkValue(path:Array<String>):Dynamic {
        var current = funkConfig;
        for (key in path) {
            if (current == null || !ReflectUtil.hasField(current, key)) return null;
            current = ReflectUtil.getField(current, key);
        }
        return current;
    }

    function setDeepFunkValue(path:Array<String>, value:Dynamic) {
        var current = funkConfig;
        for (i in 0...path.length - 1) {
            var key = path[i];
            if (current == null || !ReflectUtil.hasField(current, key)) return;
            current = ReflectUtil.getField(current, key);
        }
        if (current != null) ReflectUtil.setField(current, path[path.length - 1], value);
    }

    // Cleanup stuff
    override function close() {
        cleanup();
        super.close();
    }

    function cleanup() {
        if (configCam != null) FlxG.cameras.remove(configCam);
        FlxG.state.persistentUpdate = prevPersUpd;
        if (FlxG.state.camera.filters != null) FlxG.state.camera.filters.remove(FlxG.state.camera.filters[FlxG.state.camera.filters.length - 1]);
    }
}